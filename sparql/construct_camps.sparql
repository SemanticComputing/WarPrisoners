PREFIX wsch: <http://ldf.fi/schema/warsa/>
PREFIX wpsc: <http://ldf.fi/schema/warsa/prisoners/>
PREFIX wpri: <http://ldf.fi/warsa/prisoners/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX wgs: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
  ?sub ?p ?o ;
    skos:prefLabel ?label ;
    wgs:lat ?lat ;
    wgs:long ?long .
} WHERE {
  {
    ?sub a wsch:PowCamp ;
         ?p ?o .
    FILTER(?p != wpsc:huomautuksia-poistetaan-kun-asiat-on-kayty-lapi)
    ?sub wpsc:captivity_location ?loc .
    BIND(STRLANG(?loc, "fi") as ?label)
  }
  UNION
  {
    ?sub a wsch:PowHospital ;
         ?p ?o .
    FILTER(?p != wpsc:poistettava-tyosarake)
    OPTIONAL { ?sub wpsc:hospital_type ?type . }
    OPTIONAL { ?sub wpsc:camp_id ?camp_id . }
    BIND(STRLANG(CONCAT(COALESCE(?type, ""), IF(BOUND(?type), " ", ""), COALESCE(?camp_id, "")), "fi") as ?label)
  }
  OPTIONAL{
    ?sub wpsc:coordinates ?coord
    BIND(REPLACE(STR(?coord), "(\\d\\d\\.\\d+),.*", "$1") AS ?lat)
    BIND(REPLACE(STR(?coord), ".*,\\W*(\\d\\d\\.\\d+)$", "$1") AS ?long)
  }
}
