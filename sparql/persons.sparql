PREFIX text: <http://jena.apache.org/text#>
PREFIX apf: <http://jena.hpl.hp.com/ARQ/property#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX actors: <http://ldf.fi/schema/warsa/actors/>
PREFIX wsch: <http://ldf.fi/schema/warsa/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX cas: <http://ldf.fi/schema/narc-menehtyneet1939-45/>
PREFIX dc: <http://purl.org/dc/terms/>

SELECT DISTINCT ?id ?ngram ?label ?rank_id ?etunimet ?sukunimi ?birth_start ?birth_end ?death_start ?death_end
    ?disappearance_place ?birth_place ?occupation ?unit ?disappearance_date {

    VALUES ?ngram {
        "<VALUES>"
    }

	BIND(REPLACE(?ngram, "[-/().]", " ") AS ?ngram2)
    BIND(REPLACE(?ngram2,"([a-zåäöéèüáóA-ZÅÄÖÈÉĚȨÜÁÀÃÓÒÕ]+)", IF(?ngram = ?ngram2, "$1~", "$1~1")) AS ?qstring)

    GRAPH <http://ldf.fi/warsa/persons> { ?id text:query (?qstring 50) . }
	OPTIONAL { ?id dc:source ?source . }
  	#FILTER(IF(BOUND(?source), ?source != <http://ldf.fi/warsa/sources/source9>, 1=1))
    ?id foaf:familyName ?sukunimi .
    ?id skos:prefLabel ?label .
    OPTIONAL { ?id foaf:firstName ?etunimet . }
    OPTIONAL {
        ?promotion_id a wsch:Promotion ;
            crm:P11_had_participant ?id ;
            actors:hasRank ?rank_id .
            #OPTIONAL { ?promotion_id crm:P4_has_time-span ?timespan_id . }
    }

    OPTIONAL {
	    ?birth crm:P98_brought_into_life ?id .
	    ?birth crm:P4_has_time-span ?birth_date_id .
	    ?birth_date_id crm:P82a_begin_of_the_begin ?birth_start ;
	    crm:P82b_end_of_the_end ?birth_end .
	    OPTIONAL { ?birth dc:source ?birth_source . }
    	#FILTER(IF(BOUND(?birth_source), ?birth_source != <http://ldf.fi/warsa/sources/source9>, 1=1))
    }
    OPTIONAL {
        ?death crm:P100_was_death_of ?id .
        ?death crm:P4_has_time-span ?death_date_id .
        ?death_date_id crm:P82a_begin_of_the_begin ?death_start ;
                    crm:P82b_end_of_the_end ?death_end .
    	OPTIONAL { ?death dc:source ?death_source . }
    	#FILTER(IF(BOUND(?death_source), ?death_source != <http://ldf.fi/warsa/sources/source9>, 1=1))
    }
    OPTIONAL {
        ?id ^crm:P70_documents ?cas .
        OPTIONAL { ?cas cas:katoamispaikka ?disappearance_place . }
        OPTIONAL { ?cas cas:katoamisaika ?disappearance_date . }
        OPTIONAL { ?cas cas:synnyinkunta/skos:prefLabel ?birth_place . }
        OPTIONAL { ?cas cas:ammatti ?occupation . }
        OPTIONAL { ?cas cas:osasto ?unit . }
    }
}
