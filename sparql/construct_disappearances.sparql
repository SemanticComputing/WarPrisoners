PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wsch: <http://ldf.fi/schema/warsa/>
PREFIX wpsc: <http://ldf.fi/schema/warsa/prisoners/>
PREFIX wpri: <http://ldf.fi/warsa/prisoners/>
PREFIX wsou: <http://ldf.fi/warsa/sources/>
PREFIX wacs: <http://ldf.fi/schema/warsa/actors/>
PREFIX wact: <http://ldf.fi/warsa/actors/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dct: <http://purl.org/dc/terms/>
CONSTRUCT {
  ?disappearance a wsch:Disappearing ;
    crm:P11_had_participant ?id ;
    crm:P4_has_time-span ?vts ;
    crm:P7_took_place_at ?vp ;
    dct:source wsou:source22 ;
    skos:prefLabel ?vl_fi, ?vl_en .

  ?vts a crm:E52_Time-Span ;
    skos:prefLabel ?date_label_en, ?date_label_fi ;
    crm:P82a_begin_of_the_begin ?md ;
    crm:P82b_end_of_the_end ?md ;
    .
}
{
  ?sub wpsc:date_of_going_mia ?md_ ;
    crm:P70_documents ?id .
  OPTIONAL { ?sub wsch:given_names ?fn }
  OPTIONAL { ?sub wsch:family_name ?sn }
  FILTER NOT EXISTS { ?id ^crm:P11_had_participant/a wsch:Disappearing . }
  BIND(xsd:date(?md_) AS ?md)
  BIND(REPLACE(STR(?id), ".*(person_.+)$", "$1") AS ?no)
  FILTER(BOUND(?md))
  BIND(URI(CONCAT("http://ldf.fi/warsa/events/disappearance_", ?no)) AS ?disappearance)
  BIND(URI(CONCAT("http://ldf.fi/warsa/events/times/time_", STR(?md), "-", STR(?md))) AS ?vts)
  BIND(CONCAT(?fn, " ", ?sn) AS ?label)
  BIND(STRLANG(CONCAT(?label, " katosi"), "fi") AS ?vl_fi)
  BIND(STRLANG(CONCAT(?label, " went missing"), "en") AS ?vl_en)

  BIND(STRLANG(?md_, "en") AS ?date_label_en)
  BIND(STRLANG(?md_, "fi") AS ?date_label_fi)
}
