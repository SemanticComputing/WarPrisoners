PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wsch: <http://ldf.fi/schema/warsa/>
PREFIX wpsc: <http://ldf.fi/schema/warsa/prisoners/>
PREFIX wpri: <http://ldf.fi/warsa/prisoners/>
PREFIX wsou: <http://ldf.fi/schema/warsa/sources/>
PREFIX wacs: <http://ldf.fi/schema/warsa/actors/>
PREFIX wact: <http://ldf.fi/warsa/actors/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dct: <http://purl.org/dc/terms/>
CONSTRUCT {
  ?death a wsch:Death ;
    crm:P100_was_death_of ?id ;
    crm:P4_has_time-span ?dts ;
    crm:P7_took_place_at ?dp ;
    dct:source wsou:source_21 ;
    skos:prefLabel ?dl_fi, ?dl_en .
}
{
  ?sub wpsc:death_date ?dd_ ;
       foaf:givenName ?fn ;
       foaf:familyName ?sn ;
       crm:P70_documents ?id .
  FILTER NOT EXISTS { ?id ^crm:P100_was_death_of [] . }
  BIND(xsd:date(?dd_) AS ?dd)
  FILTER(BOUND(?dd))
  BIND(REPLACE(STR(?id), ".*(person_.+)$", "$1") AS ?no)
  BIND(URI(CONCAT("http://ldf.fi/warsa/events/death_", ?no)) AS ?death)
  BIND(CONCAT(?fn, " ", ?sn) AS ?label)
  BIND(STRLANG(CONCAT(?label, " kuoli"), "fi") AS ?dl_fi)
  BIND(STRLANG(CONCAT(?label, " died"), "en") AS ?dl_en)
  BIND(URI(CONCAT("http://ldf.fi/warsa/events/times/time_", STR(?dd), "-", STR(?dd))) AS ?dts)
}
