PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wsch: <http://ldf.fi/schema/warsa/>
PREFIX wpsc: <http://ldf.fi/schema/warsa/prisoners/>
PREFIX wpri: <http://ldf.fi/warsa/prisoners/>
PREFIX wsou: <http://ldf.fi/schema/warsa/sources/>
PREFIX wacs: <http://ldf.fi/schema/warsa/actors/>
PREFIX wact: <http://ldf.fi/warsa/actors/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dct: <http://purl.org/dc/terms/>
CONSTRUCT {
  ?promotion a wsch:Promotion ;
    crm:P11_had_participant ?id ;
    wacs:hasRank ?wr ;
    dct:source wsou:source22 ;
    skos:prefLabel ?wrl .
}
{
  ?sub wpsc:rank ?wr ;
     wsch:given_names ?fn ;
     wsch:family_name ?sn ;
     crm:P70_documents ?id .
  FILTER NOT EXISTS { ?id ^crm:P11_had_participant/wacs:hasRank ?wr . }
  ?wr skos:prefLabel ?wrl .
  BIND(REPLACE(STR(?id), ".*(person_.+)$", "$1") AS ?no)
  BIND(LCASE(REPLACE(STR(?wr), ".*/(.+?)$", "$1")) AS ?wrno)
  BIND(URI(CONCAT("http://ldf.fi/warsa/events/", ?wrno, "_", ?no)) AS ?promotion)
  BIND(CONCAT(?fn, " ", ?sn) AS ?label)
  BIND(CONCAT(?label, " - ", ?wul) AS ?jlbl)
}
