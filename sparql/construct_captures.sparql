PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wsch: <http://ldf.fi/schema/warsa/>
PREFIX wpsc: <http://ldf.fi/schema/warsa/prisoners/>
PREFIX wpri: <http://ldf.fi/warsa/prisoners/>
PREFIX wsou: <http://ldf.fi/warsa/sources/>
PREFIX wacs: <http://ldf.fi/schema/warsa/actors/>
PREFIX wact: <http://ldf.fi/warsa/actors/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dct: <http://purl.org/dc/terms/>
CONSTRUCT {
  ?capture a wsch:Capture ;
    crm:P11_had_participant ?id ;
    crm:P4_has_time-span ?cts ;
    crm:P7_took_place_at ?cp ;
    dct:source wsou:source22 ;
    skos:prefLabel ?cl_fi, ?cl_en .

  ?cts a crm:E52_Time-Span ;
    skos:prefLabel ?date_label_en, ?date_label_fi ;
    crm:P82a_begin_of_the_begin ?cd ;
    crm:P82b_end_of_the_end ?cd ;
    .
}
{
  ?sub a wsch:PrisonerRecord ;
    crm:P70_documents ?id .
  OPTIONAL { ?sub wsch:given_names ?fn }
  OPTIONAL { ?sub wsch:family_name ?sn }
  OPTIONAL { ?sub wpsc:municipality_of_capture ?cp }
  BIND(REPLACE(STR(?id), ".*(person_.+)$", "$1") AS ?no)
  BIND(URI(CONCAT("http://ldf.fi/warsa/events/capture_", ?no)) AS ?capture)
  BIND(CONCAT(COALESCE(CONCAT(?fn, " "), ""), ?sn) AS ?label)
  BIND(STRLANG(CONCAT(?label, " j√§i vangiksi"), "fi") AS ?cl_fi)
  BIND(STRLANG(CONCAT(?label, " was taken prisoner"), "en") AS ?cl_en)
  OPTIONAL {
    ?sub wpsc:date_of_capture ?cd_ .
    BIND(xsd:date(?cd_) AS ?cd)
    FILTER(BOUND(?cd))
    BIND(URI(CONCAT("http://ldf.fi/warsa/events/times/time_", STR(?cd), "-", STR(?cd))) AS ?cts)
  }
  BIND(STRLANG(?cd_, "en") AS ?date_label_en)
  BIND(STRLANG(?cd_, "fi") AS ?date_label_fi)
}
