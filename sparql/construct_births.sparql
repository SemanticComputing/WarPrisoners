PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wsch: <http://ldf.fi/schema/warsa/>
PREFIX wpsc: <http://ldf.fi/schema/warsa/prisoners/>
PREFIX wpri: <http://ldf.fi/warsa/prisoners/>
PREFIX wsou: <http://ldf.fi/schema/warsa/sources/>
PREFIX wacs: <http://ldf.fi/schema/warsa/actors/>
PREFIX wact: <http://ldf.fi/warsa/actors/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dct: <http://purl.org/dc/terms/>
CONSTRUCT {
  ?birth a wsch:Birth ;
    crm:P98_brought_into_life ?id ;
    crm:P4_has_time-span ?bts ;
    crm:P7_took_place_at ?bp ;
    dct:source wsou:source_21 ;
    skos:prefLabel ?bl_fi, ?bl_en .
}
{
  ?sub a wsch:PrisonerRecord ;
    wpsc:birth_date ?bd_ ;
    wpsc:given_name ?fn ;
    wpsc:family_name ?sn .
  FILTER NOT EXISTS { ?sub crm:P70_documents/^crm:P98_brought_into_life [] . }
  BIND(xsd:date(?bd_) AS ?bd)
  FILTER(BOUND(?bd))
  BIND(REPLACE(STR(?sub), ".*_(\\d+)$", "wp$1") AS ?no)
  BIND(URI(CONCAT("http://ldf.fi/warsa/actors/person_", ?no)) AS ?id)
  BIND(CONCAT(?fn, " ", ?sn) AS ?label)
  BIND(URI(CONCAT("http://ldf.fi/warsa/events/birth_", ?no)) AS ?birth)
  BIND(STRLANG(CONCAT(?label, " syntyi"), "fi") AS ?bl_fi)
  BIND(STRLANG(CONCAT(?label, " was born"), "en") AS ?bl_en)
  BIND(URI(CONCAT("http://ldf.fi/warsa/events/times/time_", STR(?bd), "-", STR(?bd))) AS ?bts)
}
